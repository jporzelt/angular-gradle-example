plugins {
  // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
  id "com.moowork.node" version "1.2.0"
}
final def appName = "webapp"

apply plugin: 'war'

war {
    from ("${appName}/dist")
    archiveName = "${project.name}.war"
}

node {
    // Version of node to use.
    version = '8.11.3'

    // Version of npm to use.
    //npmVersion = '5.6.0'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/${appName}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/${appName}/npm")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/${appName}")
}



npm_install {
    args = ['@angular/cli']
}

task createApp(dependsOn: 'npm_install', type: NodeTask){
    description 'Create Angular app'
    script = file("${appName}/node_modules/@angular/cli/bin/ng")
    args = ['new', appName]
}

task buildApp(dependsOn: 'npm_install', type: NodeTask){
    description 'Build Angular app'
    script = file("${appName}/node_modules/@angular/cli/bin/ng")
    args = ['build', '--prod']
    execOverrides {
        it.workingDir = appName
    }
}

clean {
    delete file("${project.buildDir}")
    delete file("${appName}/dist")
}
//test.dependsOn npm_test
war.dependsOn buildApp
build.dependsOn buildApp
task start(dependsOn: 'npm_start')
